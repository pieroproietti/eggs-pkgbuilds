#https://github.com/calamares/calamares/releases

pkgname=calamares-eggs
_pkgname=calamares
pkgver=3.3.0.r10616.11e1659ca
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('x86_64') # esclusa'i686' 
license=(GPL)
url="https://github.com/calamares/calamares/releases"
license=('LGPL')
depends=('kdbusaddons' 'kconfig' 'kcoreaddons' 'kiconthemes' 'ki18n' 'kio' 'solid' 'yaml-cpp' 'kpmcore>=4.2.0' 'mkinitcpio-openswap'
         'boost-libs' 'ckbcomp' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache' 'plasma-framework'
         'qt5-xmlpatterns' 'squashfs-tools' 'libpwquality' 'efibootmgr' 'icu' 'appstream-qt')
conflicts=('arco-calamares' 'arco-calamares-comp' 'arco-calamares-dev')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'git' 'boost')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

# posto a calamares main
source=("$_pkgname::git+https://github.com/calamares/calamares"
	"calamares-eggs.desktop"
	"calamares-eggs-debugging.desktop"
	"calamares_polkit")

sha256sums=('SKIP'
            '4e40b88a7c7f0fe0062ac24111799d03e38d058f191592284f7292edb3987264'
            '64bd1455e85e836625b828e4a9fd679e73acbdbb2a2e1e6d19a663494f016e51'
            '966c5f0834039dc6a7529e75f70417ba2510b1e643ffb49fd25855ce9dc244b4')

pkgver() {
	cd ${srcdir}/$_pkgname
	_ver="$(cat CMakeLists.txt | grep -m1 -e "set(CALAMARES_VERSION"  | xargs | sed s'/set(CALAMARES_VERSION.//g' | sed s'/)//g' | sed s'/-/./g')"
	_git=".r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
	printf "3.3.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	sed -i -e "s|\${CALAMARES_VERSION_MAJOR}.\${CALAMARES_VERSION_MINOR}.\${CALAMARES_VERSION_PATCH}|${_ver}${_git}|g" CMakeLists.txt
	sed -i -e "s|CALAMARES_VERSION_RC 1|CALAMARES_VERSION_RC 0|g" CMakeLists.txt
}

prepare() {

	cp -rv ../modules/* ${srcdir}/$_pkgname/src/modules/

	sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' "$srcdir/${_pkgname}/CMakeLists.txt"
	sed -i -e 's/# DEBUG_FILESYSTEMS/DEBUG_FILESYSTEMS/' "$srcdir/${_pkgname}/CMakeLists.txt"
	sed -i -e "s/desired_size = 512 \* 1024 \* 1024  \# 512MiB/desired_size = 512 \* 1024 \* 1024 \* 4  \# 2048MiB/" "$srcdir/${_pkgname}/src/modules/fstab/main.py"

	# add pkgrelease to patch-version
	cd ${_pkgname}
	_patchver="$(cat CMakeLists.txt | grep -m3 -e CALAMARES_VERSION_PATCH | grep -o "[[:digit:]]*" | xargs)"
	sed -i -e "s|CALAMARES_VERSION_PATCH $_patchver|CALAMARES_VERSION_PATCH $_patchver-$pkgrel|g" CMakeLists.txt
}

build() {
	cd $_pkgname

	mkdir -p build
	cd build
	cmake .. \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PYTHONQT=OFF \
	-DWITH_KF5DBus=OFF \
	-DBoost_NO_BOOST_CMAKE=ON \
	-DWEBVIEW_FORCE_WEBKIT=OFF \
	-DSKIP_MODULES="webview tracking interactiveterminal initramfs \
	initramfscfg dracut dracutlukscfg \
	dummyprocess dummypython dummycpp \
	dummypythonqt services-openrc \
	keyboardq localeq welcomeq"
	make
}

package() {
	cd ${_pkgname}/build
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/calamares-eggs.desktop" "$pkgdir/usr/share/applications/cal-arcolinux.desktop"
	install -Dm644 "$srcdir/calamares-eggs-debugging.desktop" "$pkgdir/usr/share/applications/cal-arcolinux-debugging.desktop"
	install -Dm755 "$srcdir/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	rm "$pkgdir/usr/share/applications/calamares.desktop"
}
